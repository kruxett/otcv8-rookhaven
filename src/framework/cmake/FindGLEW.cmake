# Try to find the GLEW library
#  GLEW_FOUND - system has GLEW
#  GLEW_INCLUDE_DIR - the GLEW include directory
#  GLEW_LIBRARY - the GLEW library

set(_GLEW_SEARCH_DIRS)
if(CMAKE_PREFIX_PATH)
    list(APPEND _GLEW_SEARCH_DIRS ${CMAKE_PREFIX_PATH})
endif()
if(DEFINED ENV{CMAKE_PREFIX_PATH})
    list(APPEND _GLEW_SEARCH_DIRS $ENV{CMAKE_PREFIX_PATH})
endif()

foreach(_root IN ITEMS VCPKG_INSTALLATION_ROOT VCPKG_INSTALLED_DIR)
    if(DEFINED ${_root} AND DEFINED VCPKG_TARGET_TRIPLET)
        list(APPEND _GLEW_SEARCH_DIRS "${${_root}}/installed/${VCPKG_TARGET_TRIPLET}")
    endif()
endforeach()
if(DEFINED ENV{VCPKG_ROOT} AND DEFINED VCPKG_TARGET_TRIPLET)
    list(APPEND _GLEW_SEARCH_DIRS "$ENV{VCPKG_ROOT}/installed/${VCPKG_TARGET_TRIPLET}")
endif()

list(REMOVE_DUPLICATES _GLEW_SEARCH_DIRS)

FIND_PATH(GLEW_INCLUDE_DIR NAMES GL/glew.h
          PATHS ${_GLEW_SEARCH_DIRS}
          PATH_SUFFIXES include)
SET(_GLEW_STATIC_LIBS
    libGLEW.a
    libglew32.a
    glew32s
    glew32s.lib
    glew32.lib)
SET(_GLEW_SHARED_LIBS
    libGLEW.dll.a
    libglew32.dll.a
    GLEW
    glew32
    glew32.lib)
IF(USE_STATIC_LIBS)
    FIND_LIBRARY(GLEW_LIBRARY NAMES ${_GLEW_STATIC_LIBS} ${_GLEW_SHARED_LIBS}
                 PATHS ${_GLEW_SEARCH_DIRS}
                 PATH_SUFFIXES lib lib64
                 NO_DEFAULT_PATH)
ELSE()
    FIND_LIBRARY(GLEW_LIBRARY NAMES ${_GLEW_SHARED_LIBS} ${_GLEW_STATIC_LIBS}
                 PATHS ${_GLEW_SEARCH_DIRS}
                 PATH_SUFFIXES lib lib64
                 NO_DEFAULT_PATH)
ENDIF()
if(NOT GLEW_INCLUDE_DIR OR NOT GLEW_LIBRARY)
    message(STATUS "GLEW search directories: ${_GLEW_SEARCH_DIRS}")
endif()
INCLUDE(FindPackageHandleStandardArgs)
FIND_PACKAGE_HANDLE_STANDARD_ARGS(GLEW DEFAULT_MSG GLEW_LIBRARY GLEW_INCLUDE_DIR)
MARK_AS_ADVANCED(GLEW_LIBRARY GLEW_INCLUDE_DIR)